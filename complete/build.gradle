//tag::import[]
import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStopContainer
import com.bmuschko.gradle.docker.tasks.image.DockerPullImage
//end::import[]
//tag::buildscript[]
buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "gradle.plugin.com.energizedwork.webdriver-binaries:webdriver-binaries-gradle-plugin:1.1"
        classpath "gradle.plugin.com.energizedwork:idea-gradle-plugins:1.4"
        classpath "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.14.6"
        classpath "com.bmuschko:gradle-docker-plugin:3.2.1" // <1>
    }
}
//end::buildscript[]
//tag::versiongroup[]
version "0.1"
group "demo"
//end::versiongroup[]
//tag::apply[]
apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"com.energizedwork.webdriver-binaries"
apply plugin:"com.energizedwork.idea-project-components"
apply plugin:"asset-pipeline"
apply plugin:"org.grails.grails-gsp"
apply plugin: 'com.bmuschko.docker-remote-api' // <1>
//end::apply[]

//tag::ext[]
ext {
    pgContainerName = "${System.getProperty('grails.env') ?: 'dev'}-demo-postgres"
    dbName = "${System.getProperty('grails.env') ?: 'dev'}DemoDb"
    dbPort = "${System.getProperty('grails.env')}" == 'test' ? 5433 : 5432
}
//end::ext[]

//tag::repositories[]
repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}
//end::repositories[]
//tag::dependencies[]
dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-web-boot"
    compile "org.grails:grails-logging"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-plugin-i18n"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-interceptors"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:async"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:events"
    compile "org.grails.plugins:hibernate5"
    compile "org.hibernate:hibernate-core:5.1.5.Final"
    compile "org.grails.plugins:gsp"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    //tag::postgresql[]
    provided "org.postgresql:postgresql:9.4.1211.jre7"
    //end::postgresql[]
    runtime "org.glassfish.web:el-impl:2.1.2-b03"
    runtime "com.h2database:h2"
    runtime "org.apache.tomcat:tomcat-jdbc"
    runtime "com.bertramlabs.plugins:asset-pipeline-grails:2.14.6"
    testCompile "org.grails:grails-gorm-testing-support"
    testCompile "org.grails:grails-web-testing-support"
    testCompile "org.grails.plugins:geb"
    testRuntime "org.seleniumhq.selenium:selenium-chrome-driver:3.6.0"
    testRuntime "org.seleniumhq.selenium:selenium-firefox-driver:3.6.0"
    testRuntime "org.seleniumhq.selenium:selenium-remote-driver:3.6.0"
    testRuntime "org.seleniumhq.selenium:selenium-api:3.6.0"
}
//end::dependencies[]

//end::bootRun[]
bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    addResources = true
}
//end::bootRun[]

//tag::webdriverBinaries[]
webdriverBinaries {
    chromedriver '2.32'
    geckodriver '0.18.0'
}
//end::webdriverBinaries[]

//tag::test[]
tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "webdriver.chrome.driver", System.getProperty('webdriver.chrome.driver')
    systemProperty "webdriver.gecko.driver", System.getProperty('webdriver.gecko.driver')
}
//end::test[]

//tag::assets[]
assets {
    minifyJs = true
    minifyCss = true
}
//end::assets[]

//tag::pullPostgresImage[]
task pullPostgresImage(type: DockerPullImage) {
    group = 'docker'
    description = 'Pull PostgreSQL image'
    repository = 'postgres'
    tag = '9.6.6'
}
//end::pullPostgresImage[]

//tag::createPostgresContainer[]
task createPostgresContainer(type: DockerCreateContainer, dependsOn: pullPostgresImage) {
    group = 'docker'
    description = 'Creates PostgreSQL container'
    containerName = pgContainerName
    imageId = 'postgres:9.6.6'
    portBindings = ["${dbPort}:5432"]
    env = [
        'POSTGRES_PASSWORD=dbPassword',
        "POSTGRES_DB=${dbName}",
    ] as String[]

    onError { e ->
        if (e.class.simpleName in ['BadRequestException', 'ConflictException']) {
            logger.lifecycle 'Container already exists'
        } else {
            throw e
        }
    }
}
//end::createPostgresContainer[]

//end::startPostgresContainer[]
task startPostgresContainer(type: DockerStartContainer, dependsOn: createPostgresContainer) {
    group = 'docker'
    description = 'Starts Postgres container'
    containerId = pgContainerName
    onError { e ->
        if (e.class.simpleName == 'NotModifiedException') {
            logger.lifecycle 'Container already started'
        } else {
            throw e
        }
    }
    onComplete {
        logger.lifecycle "Postgres is listening on port ${dbPort}"
    }
}
bootRun.dependsOn startPostgresContainer
//end::startPostgresContainer[]

//end::stopPostgresContainer[]
task stopPostgresContainer(type: DockerStopContainer) {
    group = 'docker'
    description = 'Stops Postgres container'
    containerId = pgContainerName
    onError { e ->
        if (e.class.simpleName == 'NotModifiedException') {
            logger.lifecycle 'Container already stopped'
        } else {
            throw e
        }
    }
}
//end::stopPostgresContainer[]

